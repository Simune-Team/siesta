CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C    This file contains routines written by A.R. Williams
C    around 1985 (possibly based on previous work).
C    They are used for the solutions of the radial Schrodinger's
C    and Poisson's equations in the atomic program.
C    It also contains some routines written by J.M.Soler
C    in collaboration with A.R.Williams and based on 
C    algorithms developed by ARW.
C    J. M. Soler, Nov. 1998.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C * The routines contained in this file are:
C
c     SUBROUTINE EGOFV
c     SUBROUTINE YOFE
c     SUBROUTINE NRMLZG
c     SUBROUTINE BCORGN
c     SUBROUTINE BCRMAX
c     SUBROUTINE NUMIN
c     SUBROUTINE NUMOUT
c     SUBROUTINE VHRTRE
c     SUBROUTINE QVLOFZ
c     SUBROUTINE LMXOFZ 
c     SUBROUTINE CNFIG 
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC




      SUBROUTINE EGOFV(H,S,N,E,G,Y,L,Z,A,B,RMAX,NPRIN,NNODE,DR)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  EIOFV DETERMINES THE EIGENENERGY AND WAVEFUNCTION CORRESPONDING
C  TO A PARTICULAR L, PRINCIPAL QUANTUM NUMBER AND BOUNDARY CONDITION.
C
C  TWO FUNDAMENTAL TECHNIQUES ARE USED TO LOCATE THE SOLUTION:
C       1) NODE COUNTING AND BISECTION
C       2) VARIATIONAL ESTIMATE BASED ON A SLOPE DISCONTINUITY IN PSI
C  THE ARGUMENTS ARE DEFINED AS FOLLOWS:
C       H,S: G" = (H-E*S)*G
C       NR: MAXIMUM ALLOWED NUMBER OF RADIAL POINTS
C       E: E(I) IS THE I-TH ENERGY FOUND
C       NE: NUMBER OF ENERGIES FOUND
C       L: THE ANGULAR MOMENTUM
C       NCOR: THE NUMBER OF LOWER-ENERGY STATES
C
C  THE INDIVIDUAL ENERGIES ARE RESOLVED BY PERFORMING A FIXED NUMBER
C  OF BISECTIONS AFTER A GIVEN EIGENVALUE HAS BEEN ISOLATED
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  Modules
C
      use precision
#ifdef MPI
      use mpi
#endif
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION H(N),S(N),G(N),Y(*)
      integer Node
#ifdef MPI
      integer MPIerror
#endif
      DATA TOL   /1.D-5/

C Get Node number
#ifdef MPI
      call MPI_Comm_Rank(MPI_Comm_World,Node,MPIerror)
#else
      Node = 0
#endif
C Added by JDG as NT was used before being initialised
      NT = 0 
      NCOR=NPRIN-L-1
      N1=NNODE
      N2=NNODE-1
      E1=E
      E2=E
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  THE LABELS 1 AND 2 REFER TO THE BISECTION PROCESS, DEFINING THE
C  RANGE IN WHICH THE DESIRED SOLUTION IS LOCATED.  THE INITIAL
C  SETTINGS OF N1, N2, E1 AND E2 ARE NOT CONSISTENT WITH THE BISECTION
C  ALGORITHM; THEY ARE SET TO CONSISTENT VALUES WHEN THE DESIRED
C  ENERGY INTERVAL HAS BEEN LOCATED.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      DEL=5.D-1
      DE=0.D0
      NITER = 0
 1    NITER = NITER + 1
      IF(NITER.GT.40) GO TO 3
      ET=E+DE
C  THE FOLLOWING LINE IS THE FUNDAMENTAL "BISECTION"
      E=0.5*(E1+E2)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  THE FOLLOWING CONCATENATION OF LOGICAL ORS ENSURES THAT NODE
C  COUNTING IS USED UNLESS THE PREVIOUS INTEGRATION OF THE RADIAL
C  EQ PRODUCED BOTH THE CORRECT NUMBER OF NODES AND A SENSIBLE
C  PREDICTION FOR THE ENERGY.
C
C     SENSIBLE MEANS THAT ET MUST BE GREATER THAN E1 AND LESS THAN E2
C     CORRECT NUMBER OF NODES MEANS THAT NT = NNODE OR NNODE-1.
C
C     LEAVING E SET TO ITS BISECTION VALUE, AND TRANSFERING TO
C     THE CALL TO YOFE MEANS THAT WE ARE PERFORMING BISECTION,
C     WHEREAS SETTING E TO ET IS USE OF THE VARIATIONAL ESTIMATE.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IF (ET.LE.E1 .OR. ET.GE.E2 .OR.
     1       NT.LT.NNODE-1 .OR. NT.GT.NNODE) GO TO 2
      E=ET
      IF(DABS(DE).LT.TOL) GO TO 6
 2    CALL YOFE(E,DE,DR,RMAX,H,S,Y,N,L,NCOR,NT,Z,A,B)
C     WRITE(6,101) L,DR,N1,NT,NNODE,N2,E1,E,E2,DE
C101  FORMAT('  L     DR     N1  NT   N  N2       E1           E',
C    1       '          E2          DE'/I3,D10.3,4I4,4F12.5)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  YOFE INTEGRATES THE SCHRO EQ.; NOW THE BISECTION LOGIC
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IF(NT.GE.NNODE) GO TO 5
C  TOO FEW NODES; SET E1 AND N1
      E1=E
      N1=NT
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  AT THIS POINT, WE HAVE JUST SET THE BOTTOM OF THE BISECTION RANGE;
C  IF THE TOP IS ALSO SET, WE PROCEDE.  IF THE TOP OF THE RANGE HAS NOT
C  BEEN SET, IT MEANS THAT WE HAVE YET TO FIND AN E GREATER THAN THE
C  DESIRED ENERGY.  THE UPPER END OF THE RANGE IS EXTENDED.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IF(N2.GE.NNODE) GO TO 1
      DEL=DEL*2.D0
      E2=E1+DEL
      GO TO 1
C  TOO MANY NODES; SET E2 AND N2
 5    E2=E
      N2=NT
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  AT THIS POINT, WE HAVE JUST SET THE TOP OF THE BISECTION RANGE;
C  IF THE TOP IS ALSO SET, WE PROCEDE.  IF THE TOP OF THE RANGE HAS
C  NOT BEEN SET, IT MEANS THAT WE HAVE YET TO FIND AN E LESS THAN THE
C  DESIRED ENERGY.  THE LOWER END OF THE RANGE IS EXTENDED.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IF(N1.LT.NNODE) GO TO 1
      DEL=DEL*2.D0
      E1=E2-DEL
      GO TO 1
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  THE NUMEROV METHOD USES A TRANSFORMATION OF THE RADIAL WAVE FCN.
C  THAT WE CALL "Y".  HAVING LOCATED THE EIGENENERGY, WE TRANSFORM
C  Y TO "G", FROM WHICH THE DENSITY IS EASILY CONSTRUCTED.
C  FINALLY, THE CALL TO "NRMLZG" NORMALIZES G TO ONE ELECTRON.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
 6    G(1) = 0.D0
      DO 7 I=2,N
           T=H(I)-E*S(I)
           G(I)=Y(I)/(1.D0-T/12.D0)
 7    CONTINUE
      CALL NRMLZG(G,S,N)
      RETURN
 3    if (Node.eq.0) WRITE(6,4) Z,L,NNODE,E,DE
 4    FORMAT(' EGOFV: TOO MANY ITERATIONS; EXECUTION STOPPING'/
     1       ' Z=',F3.0,'  L=',I2,'  NNODE=',I2,'  E=',F12.5,
     2       '  DE=',F12.5)
      STOP 8
      END





      SUBROUTINE YOFE(E,DE,DR,RMAX,H,S,Y,NMAX,L,NCOR,NNODE,Z,A,B)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   YOFE INTEGRATES THE RADIAL SCHRODINGER EQN USING THE NUMEROV
C   METHOD.
C
C   THE ARGUMENTS ARE DEFINED AS FOLLOWS:
C       E IS THE OLD ENERGY(OVERWRITTEN) BY THE NEW ENERGY
C       DE IS THE E CHANGE PREDICTED TO ELIM THE KINK IN PSI
C       DR IS THE LOG DERIV (THE BOUNDARY CONDITION)
C       G" = (H-ES)G (ALL DIAGONAL IN I (RADIUS) )
C       Y IS THE NUMEROV INDEPENDENT VARIABLE Y = G - G"/12
C       N IS THE NUMBER OF RADIAL MESH POINTS
C       L IS THE ANGULAR MOMENTUM
C       NCOR IS THE NUMBER OF STATES OF LOWER ENERGY
C       NNODE IS 1 + THE NUMBER OF INTERIOR NODES IN PSI
C       Z IS THE ATOMIC NUMBER
C       A AND B SPECIFY THE RADIAL MESH R(I)=(EXP(A*(I-1))-1)*B
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DOUBLE PRECISION H(NMAX),S(NMAX),Y(NMAX)
      ZDR = Z*A*B
      N=NMAX
 8    IF( H(N)-E*S(N) .LT. 1.D0 ) GO TO 9
      Y(N)=0.D0
      N=N-1
      GO TO 8
 9    CONTINUE
      CALL BCORGN(E,H,S,L,ZDR,Y2)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  BCORGN COMPUTES Y2, WHICH EMBODIES THE BOUNDARY CONDITION
C  SATISFIED BY THE RADIAL WAVE FUNCTION AT THE ORIGIN
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC=
      KNK=N
      CALL NUMOUT(E,H,S,Y,NCOR,KNK,NNODE,Y2,G,GSG,X)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  THE OUTWARD INTEGRATION IS NOW COMPLETE
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC=
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C     WE FIRST DECIDE IF THE KINETIC ENERGY IS SUFFICIENTLY NON
C     NEGATIVE TO PERMIT USE OF THE NUMEROV EQ AT RMAX.  IF
C     IT IS NOT, THEN ZERO-VALUE BOUNDARY CONDITION IS USED
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      YN=0.D0
      IF(N.LT.NMAX .OR. DABS(DR).GT.1.D3) GO TO 7
      CALL BCRMAX(E,DR,RMAX,H,S,N,YN,A,B)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  BCRMAX COMPUTES YN, WHICH EMBODIES THE BOUNDARY CONDITION
C  SATISFIED BY THE RADIAL WAVE FUNCTION AT RMAX
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC=
 7    CALL NUMIN(E,H,S,Y,N,NNDIN,YN,GIN,GSGIN,XIN,KNK)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  NUMIN PERFORMS THE INWARD INTEGRATION BY THE NUMEROV METHOD
C
C  THE ENERGY INCREMENT IS NOW EVALUATED FROM THE KINK IN PSI
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC=
      RATIO = G/GIN
      XIN=XIN*RATIO
      GSG=GSG+GSGIN*RATIO*RATIO
      T=H(KNK)-E*S(KNK)
      DE=G*(X+XIN+T*G)/GSG
      NNODE=NNODE+NNDIN
      IF(DE.LT.0.D0) NNODE=NNODE+1
      DO 6 I=KNK,N
         Y(I) = Y(I)*RATIO
 6    CONTINUE
      RETURN
      END



      SUBROUTINE NRMLZG(G,S,N)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   NRMLZG NORMALIZES THE SUPPLIED RADIAL WAVE FUNCTION
C
C   THE ARGUMENTS ARE DEFINED AS FOLLOWS:
C       G IS THE RADIAL WAVE FUNCTION APPROPRIATE TO THE NUMEROV
C             REPRESENTATION OF THE RADIAL SCHRODINGER EQUATION
C             THAT IS, THE RADIAL FCN R(R) = (DRDI)**1/2 G(I) / R(I)
C       G" = (H-ES)G (ALL DIAGONAL IN I (RADIUS) )
C       N1 IS THE NUMBER OF RADIAL MESH POINTS CORRESPONDING TO
C             THE PORTION OF THE RADIAL MESH ON WHICH THE NORM
C             IS DEFINED
C       N2 IS THE NUMBER OF RADIAL MESH POINTS CORRESPONDING TO
C             THE PORTION OF THE RADIAL MESH ON WHICH THE WAVE
C             FUNCTION IS DEFINED.  FOR THE INTENDED USE OF THIS
C             ROUTINE, N1 = NRVAL AND N2 = NRCOR
C       A AND B ARE THE RADIAL MESH PARAMETERS
C             R(I) = ( EXP(A*(I-1)) - 1 ) * B
C             (DR/DI = A*B AT THE ORIGIN)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC=
C
C  Modules
C
      use precision
#ifdef MPI
      use mpi
#endif
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DOUBLE PRECISION S(*),G(*),NORM
#ifdef MPI
      integer MPIerror
#endif
      integer Node

C Get Node number
#ifdef MPI
      call MPI_Comm_Rank(MPI_Comm_World,Node,MPIerror)
#else
      Node = 0
#endif
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  DETERMINE THE NORM OF G(I) USING SIMPSON'S RULE
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IF (MOD(N,2).NE.1.and.Node.eq.0) 
     .  WRITE(6,*) ' NRMLZG: N SHOULD BE ODD. N =',N
      NORM = 0.D0
      NM1 = N - 1
      DO 2 I = 2,NM1,2
         NORM=NORM + G(I)*S(I)*G(I)
 2    CONTINUE
      NORM = NORM * 2.D0
      NM2  = N - 2
      DO 3 I = 3,NM2,2
         NORM=NORM + G(I)*S(I)*G(I)
 3    CONTINUE
      NORM = NORM * 2.D0
      NM2  = N - 2
      DO 4 I = 1,N,NM1
         NORM=NORM + G(I)*S(I)*G(I)
 4    CONTINUE
      NORM = NORM/3.D0
      SRNRM = DSQRT(NORM)
      DO 5 I=1,N
         G(I) = G(I)/SRNRM
 5    CONTINUE
      RETURN
      END






      SUBROUTINE BCORGN(E,H,S,L,ZDR,Y2)
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   YOFE INTEGRATES THE RADIAL SCHRODINGER EQN USING THE NUMEROV
C   METHOD.
C
C   THE ARGUMENTS ARE DEFINED AS FOLLOWS:
C       E IS THE OLD ENERGY(OVERWRITTEN) BY THE NEW ENERGY
C       DE IS THE E CHANGE PREDICTED TO ELIM THE KINK IN PSI
C       DR IS THE LOG DERIV (THE BOUNDARY CONDITION)
C       G" = (H-ES)G (ALL DIAGONAL IN I (RADIUS) )
C       Y IS THE NUMEROV INDEPENDENT VARIABLE Y = G - G"/12
C       N IS THE NUMBER OF RADIAL MESH POINTS
C       L IS THE ANGULAR MOMENTUM
C       NNODE IS 1 + THE NUMBER OF INTERIOR NODES IN PSI
C       Z IS THE ATOMIC NUMBER
C       A AND B SPECIFY THE RADIAL MESH R(I)=(EXP(A*(I-1))-1)*B
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DOUBLE PRECISION H(*),S(*)
C
C   THE QUANTITY CALLED D(I) IN THE PROGRAM IS ACTUALLY THE INVERSE
C   OF THE DIAGONAL OF THE TRI-DIAGONAL NUMEROV MATRIX
C
      T2=H(2)-E*S(2)
      D2=-((24.D0+10.D0*T2)/(12.D0-T2))
C
C=================================================================
C  THE FOLLOWING SECTION DEALS WITH THE FACT THAT THE INDEPENDENT
C  VARIABLE "Y" IN THE NUMEROV EQUATION IS NOT ZERO AT THE ORIGIN
C  FOR L LESS THAN 2
C  THE L=0 SOLUTION G VANISHES, BUT THE FIRST AND SECOND
C  DERIVATIVES ARE FINITE, MAKING THE NUMEROV VARIABLE Y FINITE
C  THE L=1 SOLUTION G VANISHES, AND G' ALSO VANISHES, BUT
C  THE SECOND DERIVATIVE G" IS FINITE MAKING Y FINITE.  FOR L > 1,
C  G AND ITS FIRST TWO DERIVATIVES VANISH, MAKING Y ZERO.
C=================================================================
      IF(L.GE.2) GOTO 3
      IF(L.GT.0) GOTO 1
      C0=ZDR/6.D0
      C0=C0/(1.D0-0.75*ZDR)
      GO TO 2
 1    C0=1.D0/12.D0
      C0=(-C0)*8.D0/3.D0
 2    C1=C0*(12.D0+13.D0*T2)/(12.D0-T2)
      T3=H(3)-E*S(3)
      C2=(-5.D-1)*C0*(24.D0-T3)/(12.D0-T3)
      D2=(D2-C1)/(1.D0-C2)
 3    Y2=(-1.D0)/D2
      RETURN
      END



      SUBROUTINE BCRMAX(E,DR,RMAX,H,S,N,YN,A,B)
C
C 22.7.85
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DOUBLE PRECISION  H(*),S(*),
     .   E,DR,RMAX,YN,A,B,TNM1,TN,TNP1,BETA,DG,C1,C2,C3,DN
C
C     WRITE(6,*) 'BCRMAX:',DR
      TNM1=H(N-1)-E*S(N-1)
      TN  =H(N  )-E*S(N  )
      TNP1=H(N+1)-E*S(N+1)
      BETA=1.D0+B/RMAX
      DG=A*BETA*(DR+1.D0-5.D-1/BETA)
C
C
      C2=24.D0*DG/(12.D0-TN)
      DN=-((24.D0+10.D0*TN)/(12.D0-TN))
C
      C1= (1.D0-TNM1/6.D0)/(1.D0-TNM1/12.D0)
      C3=-((1.D0-TNP1/6.D0)/(1.D0-TNP1/12.D0))
      YN=-((1.D0-C1/C3)/(DN-C2/C3))
C
C
      RETURN
      END



      SUBROUTINE NUMIN(E,H,S,Y,N,NNODE,YN,G,GSG,X,KNK)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   YOFE INTEGRATES THE RADIAL SCHRODINGER EQN USING THE NUMEROV
C   METHOD.
C
C   THE ARGUMENTS ARE DEFINED AS FOLLOWS:
C       E IS THE OLD ENERGY(OVERWRITTEN) BY THE NEW ENERGY
C       DE IS THE E CHANGE PREDICTED TO ELIM THE KINK IN PSI
C       DR IS THE LOG DERIV (THE BOUNDARY CONDITION)
C       G" = (H-ES)G (ALL DIAGONAL IN I (RADIUS) )
C       Y IS THE NUMEROV INDEPENDENT VARIABLE Y = G - G"/12
C       N IS THE NUMBER OF RADIAL MESH POINTS
C       L IS THE ANGULAR MOMENTUM
C       NNODE IS 1 + THE NUMBER OF INTERIOR NODES IN PSI
C       Z IS THE ATOMIC NUMBER
C       A AND B SPECIFY THE RADIAL MESH R(I)=(EXP(A*(I-1))-1)*B
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DOUBLE PRECISION H(N),S(N),Y(N)
      Y(N)=YN
      T=H(N)-E*S(N)
      G=Y(N)/(1.D0-T/12.D0)
      GSG=G*S(N)*G
      I=N-1
      Y(I)=1.D0
      T=H(I)-E*S(I)
      G=Y(I)/(1.D0-T/12.D0)
      GSG=GSG+G*S(I)*G
      X=Y(I)-Y(N)
      NNODE=0
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  BEGIN THE INWARD INTEGRATIONBY THE NUMEROV METHOD
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC=
 1    X=X+T*G
      I=I-1
      Y(I)=Y(I+1)+X
      IF( Y(I)*Y(I+1) .LT. 0.D0) NNODE=NNODE+1
      T=H(I)-E*S(I)
      G=Y(I)/(1.D0-T/12.D0)
      GSG=GSG+G*S(I)*G
      IF(I.GT.KNK) GO TO 1
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  THE LAST STATEMENT DEFINES THE KINK RADIUS AS THE POINT WHERE
C  PSI FIRST TURNS DOWNWARD.  THIS USUALLY MEANS AT THE OUTERMOST
C  MAXIMUM
C
C  THE INWARD INTEGRATION IS NOW COMPLETE
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC=
      KNK=I
      RETURN
      END




      SUBROUTINE NUMOUT(E,H,S,Y,NCOR,KNK,NNODE,Y2,G,GSG,X)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   YOFE INTEGRATES THE RADIAL SCHRODINGER EQN USING THE NUMEROV
C   METHOD.
C
C   THE ARGUMENTS ARE DEFINED AS FOLLOWS:
C       E IS THE OLD ENERGY(OVERWRITTEN) BY THE NEW ENERGY
C       DE IS THE E CHANGE PREDICTED TO ELIM THE KINK IN PSI
C       DR IS THE LOG DERIV (THE BOUNDARY CONDITION)
C       G" = (H-ES)G (ALL DIAGONAL IN I (RADIUS) )
C       Y IS THE NUMEROV INDEPENDENT VARIABLE Y = G - G"/12
C       N IS THE NUMBER OF RADIAL MESH POINTS
C       L IS THE ANGULAR MOMENTUM
C       NNODE IS 1 + THE NUMBER OF INTERIOR NODES IN PSI
C       Z IS THE ATOMIC NUMBER
C       A AND B SPECIFY THE RADIAL MESH R(I)=(EXP(A*(I-1))-1)*B
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DOUBLE PRECISION H(KNK),S(KNK),Y(KNK)
      Y(1)=0.D0
      Y(2)=Y2
      T=H(2)-E*S(2)
      G=Y(2)/(1.D0-T/12.D0)
      GSG=G*S(2)*G
      Y(3)=1.D0
      T=H(3)-E*S(3)
      G=Y(3)/(1.D0-T/12.D0)
      GSG=GSG+G*S(3)*G
      X=Y(3)-Y(2)
      I=3
      NNODE=0
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  BEGIN THE OUTWARD INTEGRATIONBY THE NUMEROV METHOD
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC=
      NM4=KNK-4
 1    XL=X
      X=X+T*G
      I=I+1
      Y(I)=Y(I-1)+X
C     WRITE(6,300) I,Y(I),X,T,H(I),S(I)
C300  FORMAT(I5,5D14.5)
      IF( Y(I)*Y(I-1) .LT. 0.D0) NNODE=NNODE+1
      T=H(I)-E*S(I)
      G=Y(I)/(1.D0-T/12.D0)
      GSG=GSG+G*S(I)*G
      IF(I.EQ.NM4) GO TO 2
      IF(NNODE.LT.NCOR) GO TO 1
      IF(XL*X.GT.0.D0) GO TO 1
 2    KNK=I
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  THE OUTWARD INTEGRATION IS NOW COMPLETE
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC=
      RETURN
      END



      SUBROUTINE VHRTRE(RHO,V,R,DRDI,SRDRDI,NR,A)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C   VHRTRE CONSTRUCTS THE ELECTROSTATIC POTENTIAL DUE TO A SUPPLIED
C   ELECTRON DENSITY.  THE NUMEROV METHOD IS USED TO INTEGRATE
C   POISSON'S EQN.
C
C   DESCRIPTION OF ARGUMENTS:
C      RHO....4*PI*R**2 * THE ELECTRON DENSITY FOR WHICH WE CALCULATING
C             THE ELECTROSTATIC POTENTIAL
C      V......THE ELECTROSTATIC POTENTIAL DUE TO THE ELECTRON DENSITY
C             RHO.  THE CONSTANTS OF INTEGRATION ARE FIXED SO THAT THE
C             POTENTIAL TENDS TO A CONSTANT AT THE ORIGIN AND TO
C             2*Q/R AT R=R(NR), WHERE Q IS THE INTEGRATED CHARGE
C             CONTAINED IN RHO(R)
C      R......THE RADIAL MESH R(I) = B*(EXP(A(I-1))-1)
C      NR.....THE NUMBER OF RADIAL MESH POINTS
C      DRDI...DR(I)/DI
C      SRDRDI.SQRT(DR/DI)
C      A......THE PARAMETER APPEARING IN R(I) = B*(EXP(A(I-1))-1)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DOUBLE PRECISION RHO(*),V(*),R(*),DRDI(*),SRDRDI(*)
      NRM1=NR-1
      NRM2=NR-2
      A2BY4=A*A/4.D0
      YBYQ=1.D0-A*A/48.D0
      QBYY=1.D0/YBYQ
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  SIMPSON'S RULE IS USED TO PERFORM TWO INTEGRALS OVER THE ELECTRON
C  DENSITY.  THE TOTAL CHARGE QT IS USED TO FIX THE POTENTIAL AT R=R(NR)
C  AND V0 (THE INTEGRAL OF THE ELECTRON DENSITY DIVIDED BY R) FIXES
C  THE ELECTROSTATIC POTENTIAL AT THE ORIGIN
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      V0=0.D0
      QT=0.D0
      DO 1 IR=2,NRM1,2
      DZ=DRDI(IR)*RHO(IR)
      QT=QT+DZ
 1    V0=V0+DZ/R(IR)
      V0=V0+V0
      QT=QT+QT
      DO 2 IR=3,NRM2,2
      DZ=DRDI(IR)*RHO(IR)
      QT=QT+DZ
 2    V0=V0+DZ/R(IR)
      DZ=DRDI(NR)*RHO(NR)
      QT=(QT+QT+DZ)/3.D0
      V0=(V0+V0+DZ/R(NR))/3.D0
      V(1)=2.D0*V0
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  THE ELECTROSTATIC POTENTIAL AT R=0 IS SET EQUAL TO
C                       THE AVERAGE VALUE OF RHO(R)/R
C  BEGIN CONSTRUCTION OF THE POTENTIAL AT FINITE R
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IR=2
      T=SRDRDI(IR)/R(IR)
      BETA=DRDI(IR)*T*RHO(IR)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  THE NEXT 4 STATEMENTS INDICATE THAT WE FIRST FIND THE PARTICULAR
C  SOLUTION TO THE INHOMOGENEOUS EQN. FOR WHICH Q(2)=0, WE THEN
C  ADD TO THIS PARTICULAR SOLUTION A SOLUTION OF THE HOMOGENEOUS EQN.
C  (A CONSTANT IN V OR A Q PROPORTIONAL TO R)
C  WHICH WHEN DIVIDED BY R IN GOING FROM Q TO V GIVES
C  THE POTENTIAL THE DESIRED COULOMB TAIL OUTSIDE THE ELECTRON DENSITY.
C  THE SIGNIFICANCE OF THE SOLUTION VANISHING AT THE SECOND RADIAL
C  MESH POINT IS THAT, SINCE ALL REGULAR SOLUTIONS OF THE EQUATION
C  FOR Q=R*V VANISH AT THE ORIGIN, THE KNOWLEDGE OF THE SOLUTION
C  VALUE AT THE SECOND MESH POINT PROVIDES THE TWO SOLUTION VALUES
C  REQUIRED TO START THE NUMEROV PROCEDURE.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      X=0.D0
      Y=0.D0
      Q=(Y-BETA/12.D0)*QBYY
      V(IR)=2.D0*T*Q
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  BEGINNING OF THE NUMEROV ALGORITHM
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
 3    X=X+A2BY4*Q-BETA
      Y=Y+X
      IR=IR+1
      T=SRDRDI(IR)/R(IR)
      BETA=T*DRDI(IR)*RHO(IR)
      Q=(Y-BETA/12.D0)*QBYY
      V(IR)=2.D0*T*Q
      IF(IR.LT.NR) GO TO 3
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  END OF THE NUMEROV ALGORITHM
C
C  WE HAVE NOW FOUND A PARTICULAR SOLUTION TO THE INHOMOGENEOUS EQN.
C  FOR WHICH Q(R) AT THE SECOND RADIAL MESH POINT EQUALS ZERO.
C  NOTE THAT ALL REGULAR SOLUTIONS TO THE EQUATION FOR Q=R*V
C  VANISH AT THE ORIGIN.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      QPARTC = R(NR)*V(NR)/2.D0
      DZ=QT-QPARTC
      DV=2.D0*DZ/R(NR)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  THE LOOP FOLLOWING ADDS THE CONSTANT SOLUTION OF THE HOMOGENEOUS
C  EQN TO THE PARTICULAR SOLUTION OF THE INHOMOGENEOUS EQN.
C  NOTE THAT V(1) IS CONSTRUCTED INDEPENDENTLY
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      DO 4 IR=2,NR
         V(IR)=V(IR)+DV
 4    CONTINUE
      RETURN
      END



      SUBROUTINE QVLOFZ(IZ,QVAL)  

C Returns an array with the atomic ground states 
C populations.
C Input: IZ atomic number
C By J. Soler 

C
C  Modules
C
      use precision
#ifdef MPI
      use mpi
#endif

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (LMAX=3,IZMAX=98,NCHNG=15)
      DIMENSION QVAL(0:*),Q(0:LMAX,0:IZMAX),
     .          NVAL(0:5),N(0:LMAX),IZCHNG(NCHNG),LCHNG(NCHNG)

#ifdef MPI
      integer MPIerror
#endif
      integer Node

      DATA IZCHNG /3,9,11,17,19,31,35,37,49,53,55,72,81,85,87/
      DATA LCHNG  /0,0, 1, 0, 1, 2, 0, 1, 2, 0, 1, 3, 2, 0, 1/

      DATA (Q(0,I),I= 0, 2) /0.,1.,2./
      DATA (Q(0,I),I= 3,10) /1.,2.,  2.,2.,2.,2.,0.,0./
      DATA (Q(0,I),I=11,18) /1.,2.,  2.,2.,2.,2.,0.,0./
      DATA (Q(0,I),I=19,30) /1.,2.,  2.,2.,2.,1.,2.,2.,2.,2.,1.,2./
      DATA (Q(0,I),I=31,36) /        2.,2.,2.,2.,0.,0./
      DATA (Q(0,I),I=37,48) /1.,2.,  2.,2.,1.,1.,2.,1.,1.,0.,1.,2./
      DATA (Q(0,I),I=49,54) /        2.,2.,2.,2.,0.,0./
      DATA (Q(0,I),I=55,71) /1.,2.,  2.,  14*2./
      DATA (Q(0,I),I=72,80) /           2.,2.,2.,2.,2.,2.,0.,1.,2./
      DATA (Q(0,I),I=81,86) /        2.,2.,2.,2.,0.,0./
      DATA (Q(0,I),I=87,98) /1.,2.,  10*2./

      DATA (Q(1,I),I= 0,10) / 5*0.,1.,2.,3.,4.,5.,6./
      DATA (Q(1,I),I=11,18) / 2*0.,1.,2.,3.,4.,5.,6./
      DATA (Q(1,I),I=19,36) /12*0.,1.,2.,3.,4.,5.,6./
      DATA (Q(1,I),I=37,54) /12*0.,1.,2.,3.,4.,5.,6./
      DATA (Q(1,I),I=55,86) /26*0.,1.,2.,3.,4.,5.,6./
      DATA (Q(1,I),I=87,98) /12*0./

      DATA (Q(2,I),I= 0,30) /21*0.,1.,2.,3.,5.,5.,6.,7., 8.,10.,10./
      DATA (Q(2,I),I=31,48) / 8*0.,1.,2.,4.,5.,5.,7.,8.,10.,10.,10./
      DATA (Q(2,I),I=49,71) / 8*0.,1.,  6*0.,1.,6*0.,1./
      DATA (Q(2,I),I=72,80) /         2.,3.,4.,5.,6.,7.,10.,10.,10./
      DATA (Q(2,I),I=81,98) / 8*0.,1.,2.,1.,1.,3*0.,1.,2.,1./

      DATA (Q(3,I),I= 0,64) /58*0.,2.,3.,4.,5.,6.,7.,7./
      DATA (Q(3,I),I=65,71) /      9.,10.,11.,12.,13.,14.,14./
      DATA (Q(3,I),I=72,98) /18*0.,0.,2.,3.,5.,6.,7.,7.,7.,9./
 
C Get Node number
#ifdef MPI
      call MPI_Comm_Rank(MPI_Comm_World,Node,MPIerror)
#else
      Node = 0
#endif

      IF (IZ.GT.IZMAX) THEN 
        if (Node.eq.0) then
          WRITE(6,*) 'QVLOFZ: IZ GREATER THAN IZMAX'
        endif
        STOP
      ENDIF

      DO 10 L=0,LMAX
         N(L)=L+1
  10  CONTINUE
      DO 20 ICHNG=1,NCHNG
         IF (IZ.LT.IZCHNG(ICHNG)) GOTO 20
         L=LCHNG(ICHNG)
         N(L)=N(L)+1
  20  CONTINUE
      CALL LMXOFZ (IZ,LMXCHM,LMXATM)
      CALL CNFIG (IZ,NVAL)
      DO 30 L=0,LMXCHM
         IF (NVAL(L).LT.N(L)) QVAL(L)=2*(2*L+1)+Q(L,IZ)
         IF (NVAL(L).EQ.N(L)) QVAL(L)=Q(L,IZ)
         IF (NVAL(L).GT.N(L)) QVAL(L)=0
  30  CONTINUE
      END


      SUBROUTINE LMXOFZ (Z,LMXCHM,LMXATM) 

C Returns the maximum angular mometum populated in the 
C atomic ground state configuration LMXATM, and for the 
C valence shell LMXCHM
C Input: Z atomic number
C By J. Soler

      PARAMETER (NCHNG=17)
      INTEGER Z,ZCHNG(NCHNG),LCHNG(NCHNG)
      DATA ZCHNG /5,11,13,19,21,31,37,39,49,55,57,58,72,81,87,89,90/
      DATA LCHNG /1, 0, 1, 0, 2, 1, 0, 2, 1, 0, 2, 3, 2, 1, 0, 2, 3/
      LMXCHM=0
      LMXATM=0
      DO 1 ICHNG=1,NCHNG
         IF (Z.LT.ZCHNG(ICHNG)) RETURN
         LMXCHM=LCHNG(ICHNG)
         LMXATM=MAX(LMXATM,LMXCHM)
   1  CONTINUE
      RETURN
      END


      SUBROUTINE CNFIG (Z,CONFIG) 
C Returns the valence configuration for atomic ground state
C Input: Z Atomic number
C By J. Soler

      IMPLICIT INTEGER (A-Z)
      PARAMETER (LMAX=4,NCHNG=15)
      INTEGER Z,CONFIG(0:LMAX),ZCHNG(NCHNG),LCHNG(NCHNG)
*     DATA ZCHNG /3,9,11,17,19,31,35,37,49,53,55,72,81,85,87/
*     DATA ZCHNG /3,11,11,17,19,31,35,37,49,53,55,72,81,85,87/  

      DATA ZCHNG /3,11,11,19,19,31,37,37,49,55,55,72,81,87,87/
      DATA LCHNG /0,0, 1, 0, 1, 2, 0, 1, 2, 0, 1, 3, 2, 0, 1/
      DO 10 L=0,LMAX
         CONFIG(L)=L+1
   10 CONTINUE
      DO 20 ICHNG=1,NCHNG
         IF (Z.LT.ZCHNG(ICHNG)) GOTO 30
         L=LCHNG(ICHNG)
         CONFIG(L)=CONFIG(L)+1
   20 CONTINUE
   30 END




