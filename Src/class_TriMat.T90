! ---
! Copyright (C) 1996-2016	The SIESTA group
!  This file is distributed under the terms of the
!  GNU General Public License: see COPYING in the top directory
!  or http://www.gnu.org/copyleft/gpl.txt .
! See Docs/Contributors.txt for a list of contributors.
! ---

! this class aims to implement a tri-diagonal matrix in any tri-diagonal order
! The memory layout is almost equivalent to that of regular fortran
! arrays, i.e.:
!  1. the first diagonal block
!  2. the 1st lower triangular block
!  3. the 1st upper triangular block
!  4. the second diagonal block
!  5. the 2st lower triangular block
!  6. the 2st upper triangular block
!  7. etc.
!

  use alloc, only: re_alloc, de_alloc

  implicit none

  character(len=*), parameter :: mod_name="class_"//STR_TYPE_NAME//".F90"

  integer, parameter :: sp = selected_real_kind(5,10)
  integer, parameter :: dp = selected_real_kind(10,100)

  type TYPE_NAME_
     integer :: refCount = 0
     character(len=36)   :: id = "null_id"
     !----------------------
     character(len=256)  :: name = "null "//STR_TYPE_NAME
     ! The number of rows of the full matrix
     integer             :: nrows_g
     ! Number of tri-diagonal splits
     integer             :: parts
     ! Number of padding elements
     integer             :: padding

     ! Single long array to hold all tri-information
     ! Ensures locality of elements
     integer, pointer    :: tri_info(:) => null()

     ! The number of rows of the tri-diagonal parts
     integer, pointer    :: tri_nrows(:) => null()

     ! The following ** arrays are not necessary.
     ! However, they greatly speed up the execution by having
     ! quick look-up tables

     ! ** The cumultative number of rows of the tri-diagonal parts
     integer, pointer    :: tri_crows(:) => null()
     ! ** The first index of the column 
     integer, pointer    :: tri_cidx(:) => null()

#ifdef PREC
     VAR_TYPE(PREC), pointer :: mat(:) => null() ! matrix values
#else
     VAR_TYPE,       pointer :: mat(:) => null() ! matrix values
#endif
  end type

  type TYPE_NAME
     type(TYPE_NAME_), pointer :: data => null()
  end type

! Note that "basic_type.inc" adds the PRIVATE directive
! This will also release the requirement to change the local names.
! Only those through public statements should potentially be altered.

  public :: NEW_TYPE, print_type, init_val

  public :: val
  public :: index, part_index
  public :: cum_rows
  public :: nrows_g
  public :: parts, which_part
  public :: elements

  interface NEW_TYPE
     module procedure newTriMatfromDimensions
  end interface

  interface nrows_g
     module procedure nrows_gTriMat
     module procedure nrows_gTriMatPart
  end interface

  interface elements
     module procedure elements_TriMat
  end interface

  interface parts
     module procedure parts_TriMat
  end interface

  interface which_part
     module procedure which_part_TriMat
  end interface

  interface index
     module procedure index_TriMat
  end interface

  interface init_val
     module procedure initializeTriMat
  end interface

  interface val
     module procedure val_TriMat
     module procedure val_TriMat_part
  end interface

  interface cum_rows
     module procedure TriMat_crows
  end interface

  interface print_type
     module procedure printTriMat
  end interface

!========================
#include "basic_type.inc"
!========================

  subroutine delete_Data(thisData)
    type(TYPE_NAME_) :: thisData
    call de_alloc( thisData%mat, &
            name="val-"//trim(thisData%name),routine=STR_TYPE_NAME)	
    nullify(thisData%tri_nrows)
    nullify(thisData%tri_crows)
    nullify(thisData%tri_cidx)
    call de_alloc( thisData%tri_info, &
            name="info-"//trim(thisData%name),routine=STR_TYPE_NAME)
  end subroutine delete_Data

  elemental function nrows_gTriMat(this) result (n)
    type(TYPE_NAME), intent(in) :: this
    integer                     :: n
    n = this%data%nrows_g
  end function nrows_gTriMat
  elemental function nrows_gTriMatPart(this,part) result (n)
    type(TYPE_NAME), intent(in) :: this
    integer, intent(in)         :: part
    integer                     :: n
    n = this%data%tri_nrows(part)
  end function nrows_gTriMatPart

  pure function parts_TriMat(this) result (n)
    type(TYPE_NAME), intent(in) :: this
    integer                     :: n
    n = this%data%parts
  end function parts_TriMat

  elemental function which_part_TriMat(this,no) result (n)
    type(TYPE_NAME), intent(in)   :: this
    integer, intent(in) :: no
    integer             :: n
    do n = 1 , this%data%parts
       if ( no <= this%data%tri_crows(n) ) then
	  return
       end if
    end do
    n = 0
  end function which_part_TriMat

  elemental function elements_TriMat(this,all) result(el)
    type(TYPE_NAME), intent(in) :: this
    logical, intent(in), optional :: all
    integer :: el
    el = size(this%data%mat)
    if ( present(all) ) then
       if ( all ) then
      	  return
       end if
    end if
    el = el - this%data%padding
  end function elements_TriMat
  
  subroutine newTriMatFromDimensions(this,parts,tri_nrows,name,padding)
    ! This could be implemented also as an assignment 
    ! (see below)

    type(TYPE_NAME), intent(inout)  :: this
    integer,         intent(in)     :: parts, tri_nrows(parts)
    character(len=*), intent(in), optional  :: name
    integer, intent(in), optional :: padding ! padd the elements with this much

    integer :: i,n

    ! We release the previous incarnation
    ! This means that we relinquish access to the previous
    ! memory location. It will be deallocated when nobody
    ! else is using it.
   
    call init(this)

    if (present(name)) then
       this%data%name = trim(name)
    else
       this%data%name = "("//STR_TYPE_NAME//")"
    endif

    ! Number of parts of the tri-diagonality
    this%data%parts = parts

    ! Calculate number of rows
    this%data%nrows_g = sum(tri_nrows)

    ! Allocate space for all the information of the tri-diagonality
    call re_alloc(this%data%tri_info,1, 3*parts, &
         name="info-"//trim(this%data%name),routine=STR_TYPE_NAME)

    this%data%tri_nrows => this%data%tri_info(1:parts)
    this%data%tri_crows => this%data%tri_info(parts+1:2*parts)
    this%data%tri_cidx  => this%data%tri_info(2*parts+1:3*parts)

    ! Save the dimensions of the tri-diagonal parts
    this%data%tri_nrows(:) = tri_nrows(:) 

    ! Create the cumultative list
    this%data%tri_crows(1) = tri_nrows(1)
    do i = 2 , parts
       this%data%tri_crows(i) = this%data%tri_crows(i-1) + tri_nrows(i)
    end do

    ! Create the index list of the first element of a column
    this%data%tri_cidx(1) = 1
    this%data%tri_cidx(2) = this%data%tri_cidx(1) + &
            tri_nrows(1) * ( tri_nrows(1) + tri_nrows(2) )
    do i = 3 , parts
       this%data%tri_cidx(i) = this%data%tri_cidx(i-1) + &
            tri_nrows(i-1) * ( &
            tri_nrows(i-2) + &
            tri_nrows(i-1) + &
            tri_nrows(i)     )
    end do

    ! Calculate size of the tri-diagonal matrix
    n = tri_nrows(parts) ** 2
    do i = 1 , parts - 1
       n = n + tri_nrows(i)*( tri_nrows(i) + 2*tri_nrows(i+1) )
    end do
    if ( present(padding) ) then
       n = n + padding
       this%data%padding = padding
    else
       this%data%padding = 0
    end if
    if ( this%data%padding < 0 ) then
        call die('TriMat: padding is below zero. This is not allowed.')
    end if
       
    ! Allocate the full tri-diagonal matrix
    call re_alloc(this%data%mat,1,n, &
         name="val-"//trim(this%data%name),routine=STR_TYPE_NAME) 

    call tag_new_object(this)

  end subroutine newTriMatFromDimensions


  function val_TriMat(this,all) result(p)
    type(TYPE_NAME), intent(in) :: this
    logical, intent(in), optional :: all
#ifdef PREC
    VAR_TYPE(PREC), pointer     :: p(:)
#else
    VAR_TYPE,       pointer     :: p(:)
#endif
    if ( present(all) ) then
       if ( .not. all ) then
          p => this%data%mat(1:size(this%data%mat)- &
              this%data%padding)
       else
          p => this%data%mat(:)
       end if
    else
       p => this%data%mat(1:size(this%data%mat)- &
           this%data%padding)
    end if

  end function val_TriMat

  function val_TriMat_part(this,pr,pc) result(p)
    type(TYPE_NAME), intent(in) :: this
    integer, intent(in) :: pr,pc
#ifdef PREC
    VAR_TYPE(PREC), pointer     :: p(:)
#else
    VAR_TYPE,       pointer     :: p(:)
#endif
    integer :: s,e

    ! We insist on the user knowing what to do! :)
    ! Hence we don't check these things
    !if ( this%data%parts < pr .or. &
    !     this%data%parts < pc .or. &
    !     pc < pr - 1 .or. pr < pc - 1 ) then
    !   call die('Requesting invalid tri-diagonal part')
    !end if

    ! Retrieve the starting index for the column
    s = this%data%tri_cidx(pc)

    ! If the row is greater than or equal to the column
    ! Then we can add the C_pc matrix
    if ( pr >= pc ) then
       if ( pc > 1 ) &
          s = s + this%data%tri_nrows(pc-1)*this%data%tri_nrows(pc)
       if ( pr > pc ) then
          s = s + this%data%tri_nrows(pc)**2
       end if
    end if

    e = s + this%data%tri_nrows(pr)*this%data%tri_nrows(pc) - 1

    p => this%data%mat(s:e)
  end function val_TriMat_part

  function TriMat_crows(this) result(p)
    type(TYPE_NAME), intent(in) :: this
    integer, pointer :: p(:) 
    p => this%data%tri_crows
  end function TriMat_crows

  pure subroutine part_index(this, i_g, part, i_p)
    type(TYPE_NAME), intent(in) :: this
    integer, intent(in)  :: i_g
    integer, intent(out) :: part, i_p

    if ( i_g <= this%data%tri_crows(1) ) then
       part = 1
       i_p = i_g
       return
    end if

    do part = 2 , this%data%parts 
       if ( i_g <= this%data%tri_crows(part) ) then
          ! We have now found the part 
          ! which we should be in
          i_p = i_g - this%data%tri_crows(part-1)
	  return
       end if
    end do

  end subroutine part_index

! we return -1 if the index does not exist
  pure function index_TriMat(this,r,c) result(n)
    type(TYPE_NAME), intent(in) :: this
    ! The row and column requested
    integer, intent(in) :: r, c
    integer :: n
    integer :: p_c, p_r
    integer :: i_c, i_r
    
    call part_index(this,c,p_c,i_c)
    call part_index(this,r,p_r,i_r)
!    if ( p_c == 0 ) print *,'pc'
!    if ( p_r == 0 ) print *,'pr'

    n = this%data%tri_cidx(p_c) - 1 + i_r &
        + (i_c-1) * this%data%tri_nrows(p_r)

    ! If the row is greater than or equal to the column
    ! Then we can add the C_pc matrix
    if ( p_r >= p_c ) then
       if ( p_c > 1 ) &
          n = n + this%data%tri_nrows(p_c-1)*this%data%tri_nrows(p_c)
       if ( p_r > p_c ) then
          n = n + this%data%tri_nrows(p_c) ** 2
       end if
    end if

  end function index_TriMat
       
  subroutine printTriMat(this)
    type(TYPE_NAME), intent(in)  :: this
    
    if (.not. initialized(this) ) then
       print "(a)", STR_TYPE_NAME//" Not Associated"
       RETURN
    endif
    
    print "(3(a,i0),a)", "  <"//STR_TYPE_NAME//":" // trim(this%data%name) // &
                                " n_parts=",  this%data%parts, &
                                " elements=",  elements(this,all=.true.), &
                                ", refcount: ", refcount(this),">"
  end subroutine printTriMat

  subroutine initializeTriMat(this)
    type(TYPE_NAME), intent(in out) :: this
#ifdef PREC
    VAR_TYPE(PREC), pointer     :: p(:) !=> null()
#else
    VAR_TYPE,       pointer     :: p(:) !=> null()
#endif
    
    p => val(this)
!$OMP parallel workshare default(shared)
    p(:) = VAR_INIT
!$OMP end parallel workshare

  end subroutine initializeTriMat

#undef TYPE_NAME
#undef STR_TYPE_NAME
#undef TYPE_NAME_
#undef NEW_TYPE
#undef VAR_TYPE
#ifdef PREC
#undef PREC
#endif
#undef VAR_INIT
